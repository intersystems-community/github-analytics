<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="GitHub.OrganizationPage">
<Description><![CDATA[
Page github.com/<var>organization<var> with organization's and general repositories's info ]]></Description>
<Abstract>1</Abstract>
<Super>%RegisteredObject</Super>
<TimeCreated>63913,80067.816326</TimeCreated>

<Parameter name="SERVER">
<Description>
Repositories imported from</Description>
<Default>github.com</Default>
</Parameter>

<Parameter name="RepositoriesTableEnd">
<Default><![CDATA[<div class="paginate-container" data-pjax>]]></Default>
</Parameter>

<Parameter name="RepositoryBegin">
<Description>
repository's table begin</Description>
<Default><![CDATA[<div class="repo-list-item]]></Default>
</Parameter>

<Property name="request">
<Type>%Net.HttpRequest</Type>
<Private>1</Private>
</Property>

<Property name="Organization">
<Description>
Organization's name</Description>
<InitialExpression>"intersystems-ru"</InitialExpression>
</Property>

<Property name="Num">
<Description>
This page number</Description>
<InitialExpression>1</InitialExpression>
</Property>

<Property name="pos">
<Description>
current position in content</Description>
<Type>%Integer</Type>
<Private>1</Private>
</Property>

<Method name="%OnNew">
<FormalSpec>organization,num</FormalSpec>
<Private>1</Private>
<ReturnType>%Status</ReturnType>
<ServerOnly>1</ServerOnly>
<Implementation><![CDATA[
	s ..Organization = $g(organization)
	s ..Num = $g(num)
	Quit $$$OK
]]></Implementation>
</Method>

<Method name="Url">
<Description>
Build url path for current page</Description>
<CodeMode>expression</CodeMode>
<Implementation><![CDATA["/"_..Organization_"?page="_..Num
]]></Implementation>
</Method>

<Property name="Html">
<Description>
Html content of current page</Description>
<Type>%Stream.FileCharacter</Type>
<ReadOnly>1</ReadOnly>
</Property>

<Method name="HtmlGet">
<Description>
getter</Description>
<ReturnType>%Stream</ReturnType>
<Implementation><![CDATA[
	Q:'$IsObject(..request) ""
	s strm = ##class(%Stream.FileCharacter).%New()
	s sc = strm.CopyFrom( ..request.HttpResponse.Data )
	Q strm
]]></Implementation>
</Method>

<Method name="Get">
<Description>
Load page content from "github.com/{organization}?page={num}"</Description>
<FormalSpec>organization="",num=""</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	if $g(organization)'="" s ..Organization = organization
	if $g(num)'="" s ..Num = num
	
	
	s sc = ..request.Get( ..Url() ) Q:'sc sc
		
	Q 1
]]></Implementation>
</Method>

<Method name="GetPage">
<Description>
Return repositories page parser</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[organization="intersystems-ru",num="1",&sc,&session]]></FormalSpec>
<ReturnType>OrganizationPage</ReturnType>
<Implementation><![CDATA[
	s page = ..%New(organization, num)
	
	s page.request = ##class(%Net.HttpRequest).%New()
	
	s sslConfigName = ..#SERVER
	s page.request.Server = sslConfigName
	s sc = ##class(SSL.Configuration).SSLConfig( sslConfigName ) if 'sc {
		w !, "SSLConfig error: ", sc 	
	}
	s page.request.SSLConfiguration = sslConfigName
	s page.request.Https=1
	
	#define g(%prop) $g(session(%prop))	
	if $d(session) {
		s sc = page.request.InsertCookie($$$g("name"),$$$g("value"),$$$g("path"),..#SERVER,$$$g("expires"),$$$g("secure"))
		}
		
	s sc = page.Get() 
	Q:'sc ""
	
	Q page
]]></Implementation>
</Method>

<Method name="GetPages">
<Description><![CDATA[
Parse pages count from repositories page
Search line: "<div class="pagination"><span class="previous_page disabled">Previous</span> <em class="current">1</em> 
<a rel="next" href="/intersystems-ru?page=2">2</a> <a href="/intersystems-ru?page=3">3</a> 
<a href="/intersystems-ru?page=4">4</a> <a class="next_page" rel="next" href="/intersystems-ru?page=2">Next</a></div>"
And extract data from penult available page link]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>html:%Stream.FileCharacter</FormalSpec>
<ReturnType>%Integer</ReturnType>
<Implementation><![CDATA[
	
	s end = html.FindAt( 1, ..#RepositoriesTableEnd ) 
	s begin = html.FindAt( end , "<div class=""pagination"">"  )
	Q:begin=-1 1 ;if there is no pages, then it have only 1 page
	s sc = html.MoveTo( begin ) 
	s str = html.ReadLine()
	s list = $LFS( str,"</a>")
	s penult = $LG( list, $ll( list ) - 2 ) ; "<a href="/intersystems-ru?page=4">4"
	s count = $p( penult, ">", *) ;4
	Q +count
]]></Implementation>
</Method>

<Method name="Pages">
<Description>
Pages count from this page</Description>
<CodeMode>expression</CodeMode>
<Implementation><![CDATA[..GetPages( ..Html )
]]></Implementation>
</Method>

<Method name="Parse">
<Description>
Move from repositories table begin to the end of table</Description>
<Private>1</Private>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	
	s html = ..Html 
	d html.Rewind()
	s ..pos=0
	
	s pos = html.FindAt( 1, ..#RepositoryBegin ) 
    if pos = -1 {
	    s sc = $$$ERROR(,"Not found begin of repositories table") Q 0
    }
	s ..pos = pos
    s pos = pos + $l( ..#RepositoryBegin )
    
    s end = html.FindAt( pos, ..#RepositoriesTableEnd ) 
    if end = -1 {
	    s sc = $$$ERROR(,"Not found end of repositories table") Q 0
    }
	
	s sc = html.MoveTo( pos ) Q:'sc ""
	
	while pos < end {
		
		s pos = html.FindAt( pos, ..#RepositoryBegin ) Q:pos=-1
		s sc = ..repositoryBegin( pos ) Q:'sc 
		s pos = pos + $l(..#RepositoryBegin) ;offset
		
	}
	if sc && ( ..pos < end) {
		s sc = ..repositoryBegin( end ) ;for last topic	
	}
	Q sc
]]></Implementation>
</Method>

<Method name="repositoryBegin">
<Description>
found begin of repository in html content</Description>
<FormalSpec>pos:%Integer</FormalSpec>
<Private>1</Private>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	s html = ..Html 
	
	s sc = html.MoveTo( ..pos ), length = pos - ..pos 
	s repositoryHtml = html.Read( .length )
	s ..pos = pos
	
	/* Example of repository content
	<div class="repo-list-item public fork" itemprop="owns" itemscope itemtype="http://schema.org/Code">
	    <div class="repo-list-stats">
	        <span itemprop="programmingLanguage">
	          JavaScript
	        </span>
	      <a class="repo-list-stat-item tooltipped tooltipped-s" href="/intersystems/UMLExplorer/stargazers" aria-label="Stargazers">
	        <span class="octicon octicon-star "></span>
	        1
	      </a>
	      <a class="repo-list-stat-item tooltipped tooltipped-s" href="/intersystems/UMLExplorer/network"  aria-label="Forks">
	        <span class="octicon octicon-git-branch "></span>
	        3
	      </a>
	    </div>
	    <h3 class="repo-list-name">
	      <a href="/intersystems/UMLExplorer" itemprop="name codeRepository">
	        UMLExplorer</a>
	    </h3>
	      <p class="repo-list-info">
	        <span class="octicon octicon-repo-forked "></span>
	        forked from <a href="/intersystems-ru/UMLExplorer">intersystems-ru/UMLExplorer</a>
	      </p>
	      <p class="repo-list-description" itemprop="description">
	        UML Class diagram viewer for Cache classes
	      </p>
	    <p class="repo-list-meta">
	        Updated <time datetime="2015-11-06T13:32:33Z" is="relative-time">Nov 6, 2015</time>
	    </p>
	    <div class="participation-graph">
	      <poll-include-fragment src="/intersystems/UMLExplorer/graphs/participation?h=100&amp;w=640"></poll-include-fragment>
		</div>
	</div>*/
    
    s repository( "Type" ) = ..ParseType( repositoryHtml )
    s repository( "Stars" ) = ..ParseStars( repositoryHtml )
	s repository( "Forks" ) = ..ParseForks( repositoryHtml )
	s repository( "Name" ) = ..ParseName( repositoryHtml )
	s repository( "Description" ) = ..ParseDescription( repositoryHtml )
	s repository( "Updated" ) = ..ParseUpdated( repositoryHtml )
	
	
	Q ..OnRepository( .repository )
]]></Implementation>
</Method>

<Method name="ParseType">
<Description><![CDATA[
Extract private or public repository
<div class="repo-list-item public fork" itemprop="owns" itemscope itemtype="http://schema.org/Code">]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>repository:%String</FormalSpec>
<Implementation><![CDATA[
	s type = $p( repository, ..#RepositoryBegin_" ", 2 )	
	s type = $p( type, " ")
	s type = $zstrip( type, "<>WC" )
	Q type
]]></Implementation>
</Method>

<Method name="ParseStars">
<Description><![CDATA[
Extract amount of stars
<a class="repo-list-stat-item tooltipped tooltipped-s" href="/intersystems-ru/LightPivotTable/stargazers" aria-label="Stargazers">
<span class="octicon octicon-star "></span>
2
</a>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>repository:%String</FormalSpec>
<Implementation><![CDATA[
	s stars = $p( repository, "<span class=""octicon octicon-star ""></span>", 2 )	
	s stars = $p( stars, "</a>")
	s stars = $zstrip( stars, "<>WC" )
	Q +stars
]]></Implementation>
</Method>

<Method name="ParseForks">
<Description><![CDATA[
Extract amount of forks
<a class="repo-list-stat-item tooltipped tooltipped-s" href="/intersystems-ru/LightPivotTable/network"  aria-label="Forks">
<span class="octicon octicon-git-branch "></span>
3
</a>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>repository:%String</FormalSpec>
<Implementation><![CDATA[
	s forks = $p( repository, "<span class=""octicon octicon-git-branch ""></span>", 2 )	
	s forks = $p( forks, "</a>")
	s forks = $zstrip( forks, "<>WC" )
	Q +forks
]]></Implementation>
</Method>

<Method name="ParseName">
<Description><![CDATA[
Extract name of repository
<h3 class="repo-list-name">
<a href="/intersystems-ru/LightPivotTable" itemprop="name codeRepository">
LightPivotTable</a>
</h3>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>repository:%String</FormalSpec>
<Implementation><![CDATA[
	s name = $p( repository, "<h3 class=""repo-list-name"">", 2 )	
	s name = $p( name, "</a>")
	s name = $p( name, ">",2)
	s name = $zstrip( name, "<>WC" )
	Q name
]]></Implementation>
</Method>

<Method name="ParseDescription">
<Description><![CDATA[
Extract description
<p class="repo-list-description" itemprop="description">
Lightweight pivot table representation for MDX2JSON source for InterSystems Cache
</p>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>repository:%String</FormalSpec>
<Implementation><![CDATA[
	s description = $p( repository, "itemprop=""description"">", 2 )	
	s description = $p( description, "</p>")
	s description = $zstrip( description, "<>WC" )
	Q description
]]></Implementation>
</Method>

<Method name="ParseUpdated">
<Description><![CDATA[
Extract date and time of last update
Make it Moscow timezone and convert it odbc format
<p class="repo-list-meta">
Updated <time datetime="2015-12-19T21:59:35Z" is="relative-time">Dec 19, 2015</time>
</p>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>repository:%String</FormalSpec>
<Implementation><![CDATA[
	s updated = $p( repository, "<time datetime=""", 2)
	s updated = $p( updated, "Z""") ;2015-12-19T21:59:35
	
	s date = $p( updated,"T"), date = $zstrip(date, "<>WC" ) ;2015-12-19
	s time = $p( updated,"T",2), time = $zstrip(time, "<>WC" ) ;21:59:35
	
	s dateh = $zdh( date, 3) 
	s timeh = $zth( time )
	
	#; Making Moscow timezone, eg adding 3 hours
	if timeh >= 75600{			;if time more than 21:00:00 
		s timeh = timeh - 75600 ;minus 21 hours
		s dateh = dateh + 1}
	else{						;if time fewer than 21:00:00
		s timeh = timeh + 10800	;plus 3 hours
		}
	
	s ts = $zdt( dateh_","_timeh, 3 ) ;odbc timestamp
	Q ts
]]></Implementation>
</Method>

<Method name="OnRepository">
<Description><![CDATA[
Event handler 
<var>data</var> - key-value array with repository/organization properties 
If handler return 0, parsing will be interrupted]]></Description>
<Abstract>1</Abstract>
<FormalSpec><![CDATA[&data]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	Q 1
]]></Implementation>
</Method>

<Method name="Scan">
<Description>
Example scan across all forum topic pages</Description>
<Abstract>1</Abstract>
<ClassMethod>1</ClassMethod>
<FormalSpec>organization="inersistems-ru"</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	Q 1
]]></Implementation>
</Method>

<Parameter name="OrganizationBegin">
<Description>
organization's table begin</Description>
<Default><![CDATA[<header class="org-header">]]></Default>
</Parameter>

<Parameter name="OrganizationEnd">
<Description>
organization's table end</Description>
<Default><![CDATA[</header>]]></Default>
</Parameter>

<Method name="ParseOrganization">
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[

	s html = ..Html 
	d html.Rewind()
	
	s pos = html.FindAt( 1, ..#OrganizationBegin ) 
    if pos = -1 {
	    s sc = $$$ERROR(,"Not found begin of organization's table") Q 0
    }
    s pos = pos + $l( ..#OrganizationBegin )
    
    s end = html.FindAt( pos, ..#OrganizationEnd ) 
    if end = -1 {
	    s sc = $$$ERROR(,"Not found end of organization's table") Q 0
    }
	
	s sc = html.MoveTo( pos ) Q:'sc 0
	s length = end - pos 
	s organizationHtml = html.Read( .length )
	
	/*Example of organization content
	<header class="org-header">
	  <div class="container">
	    <div class="flex-table org-header-wrapper">
	      <img alt="@intersystems-ru" class="flex-table-item avatar" height="100" itemprop="image" src="https://avatars0.githubusercontent.com/u/2911705?v=3&amp;s=200" width="100" />
	      <div class="flex-table-item flex-table-item-primary">
	        <h1 class="org-name">
	          <span>InterSystems Corp.</span>
	        </h1>
	          <ul class="org-header-meta has-blog has-email">
	              <li class="meta-item">
	                <span class="octicon octicon-link "></span>
	                <a href="http://www.intersystems.com/" class="meta-link" itemprop="url" title="http://www.intersystems.com/">http://www.intersystems.com/</a>
	              </li>
	              <li class="meta-item">
	                <span class="octicon octicon-mail "></span>
	                <a class="meta-link" href="&#109;&#97;&#105;&#108;&#116;&#111;&#58;%69%6e%66%6f@%69%6e%74%65%72%73%79%73%74%65%6d%73.%72%75" itemprop="email">info@intersystems.ru</a>
	              </li>
	          </ul>
	      </div>
	    </div>
	    <nav class="pagehead-tabs" role="navigation">
	  <a class="pagehead-tabs-item selected" href="/intersystems-ru">
	    <span class="octicon octicon-repo "></span>
	    Repositories
	  </a>
	    <a class="pagehead-tabs-item " href="/orgs/intersystems-ru/people">
	      <span class="octicon octicon-organization "></span>
	      People
	      <span class="counter">75</span>
	    </a>
	    <a class="pagehead-tabs-item " href="/orgs/intersystems-ru/teams">
	      <span class="octicon octicon-jersey "></span>
	      Teams
	      <span class="counter">3</span>
	    </a>
	</nav>
	  </div>
	</header>*/
	
    if $f(organizationHtml,"<h1 class=""org-name"">") {
		s organization("FullName") = ..ParseOrgFullName(organizationHtml)
    	}
    if $f(organizationHtml,"<p class=""org-description""") {
		s organization("Description") = ..ParseOrgDescription(organizationHtml)
    	}
    if $f(organizationHtml,"has-blog") {
		s organization("OrgLink") = ..ParseOrgLink(organizationHtml)
    	}
    if $f(organizationHtml,"has-email") {
		s organization("eMail") = ..ParseOrgEMail(organizationHtml)
    	}
    if $f(organizationHtml,"People") {
	    s organization("People") = ..ParsePeople(organizationHtml)
    	}
    	
	Q ..OnOrganization( .organization )
]]></Implementation>
</Method>

<Method name="ParseOrgFullName">
<Description><![CDATA[
Extract full name of organization
<h1 class="org-name">
<span>InterSystems Corp.</span>
</h1>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>organization:%String</FormalSpec>
<Implementation><![CDATA[
	s name = $p( organization, "<h1 class=""org-name"">", 2 )	
	s name = $p( name, "</span>")
	s name = $p( name, "<span>",2)
	s name = $zstrip( name, "<>WC" )
	Q name
]]></Implementation>
</Method>

<Method name="ParseOrgDescription">
<Description><![CDATA[
Extract description of organization
<p class="org-description" itemprop="description">Repositories related to the Python Programming language</p>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>organization:%String</FormalSpec>
<Implementation><![CDATA[
	s description = $p( organization, "itemprop=""description"">", 2 )	
	s description = $p( description, "</p>")
	s description = $zstrip( description, "<>WC" )
	Q description
]]></Implementation>
</Method>

<Method name="ParseOrgLink">
<Description><![CDATA[
Extract link of organization
<li class="meta-item">
<span class="octicon octicon-link "></span>
<a href="http://www.intersystems.com/" class="meta-link" itemprop="url" title="http://www.intersystems.com/">http://www.intersystems.com/</a>
</li>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>organization:%String</FormalSpec>
<Implementation><![CDATA[
	s link = $p( organization, "octicon-link ""></span>", 2 )	
	s link = $p( link, "</a>")
	s link = $p( link, ">", 2)
	s link = $zstrip( link, "<>WC" )
	Q link
]]></Implementation>
</Method>

<Method name="ParseOrgEMail">
<Description><![CDATA[
Extract eamil of organization
<li class="meta-item">
<span class="octicon octicon-mail "></span>
<a class="meta-link" href="&#109;&#97;&#105;&#108;&#116;&#111;&#58;%69%6e%66%6f@%69%6e%74%65%72%73%79%73%74%65%6d%73.%72%75" itemprop="email">info@intersystems.ru</a>
</li>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>organization:%String</FormalSpec>
<Implementation><![CDATA[
	s eMail = $p( organization, "octicon-mail ""></span>", 2 )	
	s eMail = $p( eMail, "</a>")
	s eMail = $p( eMail, ">", 2)
	s eMail = $zstrip( eMail, "<>WC" )
	Q eMail
]]></Implementation>
</Method>

<Method name="ParsePeople">
<Description><![CDATA[
Extract amount of organization's people
<a class="pagehead-tabs-item " href="/orgs/intersystems-ru/people">
<span class="octicon octicon-organization "></span>
People
<span class="counter">75</span>
</a>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>organization:%String</FormalSpec>
<Implementation><![CDATA[
	s people = $p( organization, "People", 2 )	
	s people = $p( people, "</span>")
	s people = $p( people, ">", 2)
	s people = $zstrip( people, "<>WC" )
	Q +people
]]></Implementation>
</Method>

<Method name="OnOrganization">
<Description><![CDATA[
Event handler 
<var>data</var> - key-value array with organization properties 
If handler return 0, parsing will be interrupted]]></Description>
<Abstract>1</Abstract>
<FormalSpec><![CDATA[&data]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	Q 1
]]></Implementation>
</Method>
</Class>
</Export>
