<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="GitHub.RepositoryPage">
<Description><![CDATA[
Page github.com/<var>organization<var>/<var>repository<var> with detail repository info]]></Description>
<Super>%RegisteredObject</Super>
<TimeCreated>63913,80285.795625</TimeCreated>

<Parameter name="SERVER">
<Description>
Repositories imported from</Description>
<Default>github.com</Default>
</Parameter>

<Parameter name="DetailTableBegin">
<Description>
repository's detail info table begin</Description>
<Default><![CDATA[<ul class="pagehead-actions">]]></Default>
</Parameter>

<Parameter name="DetailTableEnd">
<Description>
repository's detail info table end</Description>
<Default><![CDATA[<div class="file-navigation in-mid-page file-navigation-new">]]></Default>
</Parameter>

<Property name="request">
<Type>%Net.HttpRequest</Type>
<Private>1</Private>
</Property>

<Property name="Organization">
<Description>
Organization's name</Description>
<InitialExpression>"intersystems-ru"</InitialExpression>
</Property>

<Property name="Repository">
<Description>
Repository's name</Description>
</Property>

<Method name="%OnNew">
<FormalSpec>organization,repository</FormalSpec>
<Private>1</Private>
<ReturnType>%Status</ReturnType>
<ServerOnly>1</ServerOnly>
<Implementation><![CDATA[
	set ..Organization = $get(organization)
	set ..Repository = $get(repository)
	Quit $$$OK
]]></Implementation>
</Method>

<Method name="Url">
<Description>
Build url path for current page</Description>
<CodeMode>expression</CodeMode>
<Implementation><![CDATA["/"_..Organization_"/"_..Repository
]]></Implementation>
</Method>

<Property name="Html">
<Description>
Html content of current page</Description>
<Type>%Stream.FileCharacter</Type>
<ReadOnly>1</ReadOnly>
</Property>

<Method name="HtmlGet">
<Description>
getter</Description>
<ReturnType>%Stream</ReturnType>
<Implementation><![CDATA[
	Quit:'$IsObject(..request) ""
	set strm = ##class(%Stream.FileCharacter).%New()
	set sc = strm.CopyFrom( ..request.HttpResponse.Data )
	Quit strm
]]></Implementation>
</Method>

<Method name="Get">
<Description>
Load page content from "github.com/{organization}/{repository}"</Description>
<FormalSpec>organization="",repositiry=""</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	if $get(organization)'="" set ..Organization = organization
	if $get(repositiry)'="" set ..Repository = repositiry
	
	
	set sc = ..request.Get( ..Url() ) Quit:'sc sc
		
	Quit 1
]]></Implementation>
</Method>

<Method name="GetPage">
<Description>
Return repository's detail info page parser</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[organization="intersystems-ru",repository="",&sc,&session]]></FormalSpec>
<ReturnType>RepositoryPage</ReturnType>
<Implementation><![CDATA[
	set page = ..%New(organization, repository)
	
	set page.request = ##class(%Net.HttpRequest).%New()
	
	set sslConfigName = ..#SERVER
	set page.request.Server = sslConfigName
	set sc = ##class(SSL.Configuration).SSLConfig( sslConfigName ) if 'sc {
		write !, "SSLConfig error: ", sc 	
	}
	set page.request.SSLConfiguration = sslConfigName
	set page.request.Https=1
	
	#define g(%prop) $get(session(%prop))	
	if $get(session("name"))'="" {
		set sc = page.request.InsertCookie($$$g("name"),$$$g("value"),$$$g("path"),..#SERVER,$$$g("expires"),$$$g("secure"))
		}
		
	set:repository'="" sc = page.Get() 
	Quit:'sc ""
	
	Quit page
]]></Implementation>
</Method>

<Method name="OnDetailInfo">
<Description><![CDATA[
Event handler 
<var>data</var> - key-value array with repository's detail properties 
If handler return 0, parsing will be interrupted]]></Description>
<Abstract>1</Abstract>
<FormalSpec><![CDATA[&data]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	Quit 1
]]></Implementation>
</Method>

<Method name="Scan">
<Description>
Example scan across all forum topic pages</Description>
<Abstract>1</Abstract>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&data]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	Quit 1
]]></Implementation>
</Method>

<Method name="ParseDetailInfo">
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[

	set html = ..Html 
	do html.Rewind()
	
	Quit:html.FindAt(1, "<!-- /.empty-repo -->")'=-1 "Empty repo"
	
	set pos = html.FindAt( 1, ..#DetailTableBegin ) 
    if pos = -1 {
	    set sc = $$$ERROR(,"Not found begin of repository detail info table") Quit 0
    }
    set pos = pos + $length( ..#DetailTableBegin )
    
    set end = html.FindAt( pos, ..#DetailTableEnd ) 
    if end = -1 {
	    set sc = $$$ERROR(,"Not found end of repository detail info table") Quit 0
    }
	
	set sc = html.MoveTo( pos ) Quit:'sc 0
	set length = end - pos 
	set detailInfoHtml = html.Read( .length )
	
	
	set detailInfo("Watchers") = ..ParseWatchers(detailInfoHtml)
	set detailInfo("Issues") = ..ParseIssues(detailInfoHtml)
	
	set detailInfo("Commits") = ..ParseStats(detailInfoHtml,1)
	set detailInfo("Branches") = ..ParseStats(detailInfoHtml,2)
	set detailInfo("Releases") = ..ParseStats(detailInfoHtml,3)
	
	//Amount of contributors might be in URI, that cointain only the following
	//<a href="/intersystems-ru/LightPivotTable/graphs/contributors">
	//	<span class="octicon octicon-organization "></span>
	//	<span class="num text-emphasized">
	//		2
	//	</span>
	//	contributors
	//</a>
	if $find(detailInfoHtml,"/contributors_size"">"){
		set sc = ..request.Get(..Url()_"/contributors_size")
		set tmpStrm = ##class(%Stream.FileCharacter).%New()
		set sc = tmpStrm.CopyFrom( ..request.HttpResponse.Data )
		set tmpHtml = tmpStrm.Read()
		set detailInfo("Contributors") = ..ParseStats(tmpHtml,4)
		kill tmpHtml, tmpStrm
		}
	else{
		set detailInfo("Contributors") = ..ParseStats(detailInfoHtml,4)
		}
		
	if $find(detailInfoHtml,"<div class=""repository-lang-stats-graph") {
		set sc = ..ParseLanguageDetails(detailInfoHtml, .languageDetails )
		merge detailInfo("LanguageDetails") = languageDetails
    	}
    	
	Quit ..OnDetailInfo( .detailInfo )
]]></Implementation>
</Method>

<Method name="ParseWatchers">
<Description><![CDATA[
Extract amount of watchers
<span class="octicon octicon-eye"></span>
   Watch
 </a>
 <a class="social-count" href="/python/pythondotorg/watchers">
   75
 </a>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>detailInfoHtml:%String</FormalSpec>
<Implementation><![CDATA[
	set watchers = $piece( detailInfoHtml, "/watchers"">", 2 )	
	set watchers = $piece( watchers, "</a>")
	set watchers = $zstrip( watchers, "<>WC" )
	Quit +watchers
]]></Implementation>
</Method>

<Method name="ParseIssues">
<Description><![CDATA[
Extract amount of issues
<span class="octicon octicon-issue-opened "></span>
Issues
<span class="counter">127</span>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>detailInfoHtml:%String</FormalSpec>
<Implementation><![CDATA[
	set issues = $piece( detailInfoHtml, "<span class=""octicon octicon-issue-opened "">", 2 )	
	set issues = $piece( issues, "</span>",2)
	set issues = $piece( issues, ">",2)
	set issues = $zstrip( issues, "<>WC" )
	Quit +issues
]]></Implementation>
</Method>

<Method name="ParseStats">
<Description><![CDATA[
Extract amount of commits(i = 1), branches(i = 2), releases(i = 3), contributors(i = 4)
Number of this four stats starts after "<span class="num text-emphasized">"
Example on commits
<li class="commits">
<a data-pjax href="/python/pythondotorg/commits/master">
<span class="octicon octicon-history "></span>
<span class="num text-emphasized">
1,349
</span>
commits
</a>
</li>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>detailInfoHtml:%String,iStat</FormalSpec>
<Implementation><![CDATA[
	set num = iStat + 1
	set stats = $piece( detailInfoHtml, "<span class=""num text-emphasized"">", num )	
	set stats = $piece( stats, "</span>")
	set stats = $zstrip( stats, "<>WC" )
	set stats = $p(stats,",")_$p(stats,",",2)_$p(stats,",",3)
	Quit +stats		//Fetching contributors - sc.plugin, iknowSocial
]]></Implementation>
</Method>

<Method name="ParseLanguageDetails">
<Description><![CDATA[
Extract description of detailInfoHtml
<div class="repository-lang-stats-graph js-toggle-lang-stats" title="Click for language details">
   <span class="language-color" aria-label="Python 30.4%" style="width:30.4%; background-color:#3572A5;" itemprop="keywords">Python</span>
   <span class="language-color" aria-label="HTML 28.1%" style="width:28.1%; background-color:#e44b23;" itemprop="keywords">HTML</span>
   <span class="language-color" aria-label="JavaScript 24.5%" style="width:24.5%; background-color:#f1e05a;" itemprop="keywords">JavaScript</span>
   <span class="language-color" aria-label="CSS 15.3%" style="width:15.3%; background-color:#563d7c;" itemprop="keywords">CSS</span>
   <span class="language-color" aria-label="PostScript 1.5%" style="width:1.5%; background-color:#ccc;" itemprop="keywords">PostScript</span>
   <span class="language-color" aria-label="Ruby 0.2%" style="width:0.2%; background-color:#701516;" itemprop="keywords">Ruby</span>
</div>	]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>detailInfoHtml:%String,*languageDetails</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set start = $find(detailInfoHtml, "<div class=""repository-lang-stats-graph")
	set end = $find(detailInfoHtml, "</div>", start) - ($length("</div>")+1)
	set languageDetailsHtml = $extract(detailInfoHtml,start,end)
	
	set pos = $find(languageDetailsHtml,"<span class=""language-color""")
	set num = 0
	while pos{
		set num = num + 1
		set pos = $find(languageDetailsHtml,"<span class=""language-color""",pos)
		set languageDetails(num,"Language") = ..ParseLanguage(languageDetailsHtml, num)
		set languageDetails(num,"Percent") = ..ParsePercent(languageDetailsHtml, num)
		}
	set languageDetails("Amount") = num
	Quit 1
]]></Implementation>
</Method>

<Method name="ParseLanguage">
<Description><![CDATA[
Extract language's name
<span class="language-color" aria-label="Python 30.4%" style="width:30.4%; background-color:#3572A5;" itemprop="keywords">Python</span>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>languageDetailsHtml,num</FormalSpec>
<Implementation><![CDATA[
	set num = num + 1
	set language = $piece( languageDetailsHtml, "itemprop=""keywords"">", num)	
	set language = $piece( language, "</span>")
	set language = $zstrip( language, "<>WC" )
	Quit language
]]></Implementation>
</Method>

<Method name="ParsePercent">
<Description><![CDATA[
Extract language's percent
<span class="language-color" aria-label="Python 30.4%" style="width:30.4%; background-color:#3572A5;" itemprop="keywords">Python</span>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>languageDetailsHtml,num</FormalSpec>
<Implementation><![CDATA[
	set num = num + 1
	set percent = $piece( languageDetailsHtml, "style=""width:", num)	
	set percent = $piece( percent, "%")
	set percent = $zstrip( percent, "<>WC" )
	Quit percent
]]></Implementation>
</Method>
</Class>
</Export>
